## 实验源码
```asm
;=========================================================
; 文件名: A82541.ASM
; 功能描述: 通过对计数器 0 进行计数, 计数初值为 4, 
;           当计数满后，产生正跳变触发中断，中断
;           程序显示 M (每按 5 次输出一个 M)
;=========================================================

IOY0     EQU  0600H        		;IOY0起始地址
A8254    EQU  IOY0+00H*2
B8254    EQU  IOY0+01H*2
C8254    EQU  IOY0+02H*2
CON8254  EQU  IOY0+03H*2

SSTACK	SEGMENT STACK
		DW 32 DUP(?)
SSTACK	ENDS

CODE	SEGMENT
	ASSUME CS:CODE, SS:SSTACK
START:	PUSH DS
	MOV AX, 0000H
	MOV DS, AX
	MOV AX, OFFSET IRQ7			;取中断入口地址
	MOV SI, 003CH				;中断矢量地址
	MOV [SI], AX				;填 IRQ7 的偏移矢量
	MOV AX, CS					;段地址
	MOV SI, 003EH
	MOV [SI], AX				;填 IRQ7 的段地址矢量
	CLI
	POP DS

	;初始化主片8259
	MOV AL, 11H
	OUT 20H, AL				;ICW1
	MOV AL, 08H
	OUT 21H, AL				;ICW2
	MOV AL, 04H
	OUT 21H, AL				;ICW3
	MOV AL, 01H
	OUT 21H, AL				;ICW4
	MOV AL, 6FH				;OCW1
	OUT 21H, AL

	;8254
	MOV DX, CON8254
	MOV AL, 10H				;计数器 0，方式 0
	OUT DX, AL
	MOV DX, A8254
	MOV AL, 04H
	OUT DX, AL
	STI
AA1:	
    JMP AA1
IRQ7:	
    MOV DX, A8254           ;重新对计数器 0 赋初值 4
	MOV AL, 04H
	OUT DX, AL
	MOV AX, 014DH
	INT 10H					;显示字符 M
	MOV AX, 0120H
	INT 10H		
	MOV AL, 20H
	OUT 20H, AL				;中断结束命令
	IRET
CODE	ENDS
END  START
```

## 要点分析

### 8254 端口地址的初始化
- IOY0: 起始地址 0600H, 为 8254 芯片的基地址, 该基地址并不是 x86 架构中规定的固定地址, 其通常在硬件设计时就已经确定, 在硬件制造过程中被固化
- A8254、B8254、C8254、CON8254 都是基于 IOY0 并添加偏移地址后形成的 I/O 地址
```asm
IOY0     EQU  0600H        ; IOY0起始地址
A8254    EQU  IOY0+00H*2   ; Counter 0 端口地址
B8254    EQU  IOY0+01H*2   ; Counter 1 端口地址
C8254    EQU  IOY0+02H*2   ; Counter 2 端口地址
CON8254  EQU  IOY0+03H*2   ; 控制寄存器端口地址
```

### 8254 的初始化
- 在 x86 架构中，I/O 端口地址是 16 位的，因此地址空间是 64KB, 与中断向量表(内存地址)的 20 位地址存在区别
- **I/O 指令**: 指令如 IN 和 OUT 是专门用来访问 I/O 端口的, 这些指令使用的地址都是16位的, 例如 8259 对 ICW1 的设置 `OUT 20H, AL`, 其中的 `20H` 便是 I/O 端口地址(16 位); 8254 对控制寄存器的设置 `MOV DX, CON8254 : MOV AL, 10H : OUT DX, AL`, 其中 `DX` 寄存器中存储的便是 I/O 端口地址(16位)
- 该 8254 的初始化涉及两点: 一是写入方式字(控制寄存器), 二是写入计数器的计数初值
```asm
;8254
MOV DX, CON8254
MOV AL, 10H				;计数器 0，方式 0
OUT DX, AL
MOV DX, A8254
MOV AL, 04H
OUT DX, AL
STI
```

### 中断过程分析
<img src="./images/8254 计数应用实验接线图.png" alt="8254 linking diagram" width="50%">

- **初始值**: 计数器 0 的初始值设置为 4
- **计数过程**: 每按一次开关，计数器减少 1, 从 4 减到 0，共需要 5 次操作（包括 0 在内）
    - 第 1 次按下开关：计数器值从 4 变为 3
    - 第 2 次按下开关：计数器值从 3 变为 2
    - 第 3 次按下开关：计数器值从 2 变为 1
    - 第 4 次按下开关：计数器值从 1 变为 0
    - 第 5 次按下开关：计数器值从 0 变为计数结束，触发中断。

## 附加要求
- 将 8254 的计数器 0 设置为方式 0，计数值为十进制数 4，用单次脉冲 KK1＋ 作为 CLK0 时钟，OUT0 连接 MIR7
- 每当 KK1＋按动 5 次后产生中断请求，在屏幕上显示字符 "vanCount4、vanCount3……"
```asm
;=========================================================
; 文件名: A82541.ASM
; 功能描述: 通过对计数器 0 进行计数, 计数初值为 4, 
;           当计数满后，产生正跳变触发中断，中断
;           程序显示 vanCountx，其中 x 值递减显示。
;=========================================================
IOY0     EQU  0600H        ;IOY0起始地址
A8254    EQU  IOY0+00H*2
B8254    EQU  IOY0+01H*2
C8254    EQU  IOY0+02H*2
CON8254  EQU  IOY0+03H*2

SSTACK SEGMENT STACK
DW 32 DUP(?)
SSTACK ENDS

CODE SEGMENT
ASSUME CS:CODE, SS:SSTACK

START:
    PUSH DS
    MOV AX, 0000H
    MOV DS, AX
    MOV AX, OFFSET IRQ7
    MOV SI, 003CH
    MOV [SI], AX
    MOV AX, CS
    MOV SI, 003EH
    MOV [SI], AX
    CLI
    POP DS

; 初始化主片 8259
    MOV AL, 11H
    OUT 20H, AL
    MOV AL, 08H
    OUT 21H, AL
    MOV AL, 04H
    OUT 21H, AL
    MOV AL, 01H
    OUT 21H, AL
    MOV AL, 6FH
    OUT 21H, AL

; 初始化 8254
    MOV DX, CON8254
    MOV AL, 10H
    OUT DX, AL
    MOV DX, A8254
    MOV AL, 04H
    OUT DX, AL
    STI

AA1:
    JMP AA1

; 定义计数变量
    COUNTER DB 0

IRQ7:
    ; 计数器 0 初值重新赋值为 4
    MOV DX, A8254
    MOV AL, 04H
    OUT DX, AL
    
    ; 更新计数变量
    INC BYTE PTR COUNTER
    MOV AL, COUNTER
    CMP AL, 5
    JNE NO_RESET
    MOV BYTE PTR COUNTER, 1

NO_RESET:
    ; 显示 "vancount"
    MOV AX, 0176H       ; 显示字符 'v'
    INT 10H
    MOV AX, 0161H       ; 显示字符 'a'
    INT 10H
    MOV AX, 016EH       ; 显示字符 'n'
    INT 10H
    MOV AX, 0143H       ; 显示字符 'C'
    INT 10H
    MOV AX, 016FH       ; 显示字符 'o'
    INT 10H
    MOV AX, 0175H       ; 显示字符 'u'
    INT 10H
    MOV AX, 016EH       ; 显示字符 'n'
    INT 10H
    MOV AX, 0174H       ; 显示字符 't'
    INT 10H

    ; 计算并显示x值
    MOV AL, COUNTER
    CMP AL, 1
    JE SHOW_4
    CMP AL, 2
    JE SHOW_3
    CMP AL, 3
    JE SHOW_2
    CMP AL, 4
    JE SHOW_1
    JMP DONE
    
SHOW_4:
    MOV AX, 0134H       ; 显示字符 '4'
    INT 10H
    JMP DONE

SHOW_3:
    MOV AX, 0133H       ; 显示字符 '3'
    INT 10H
    JMP DONE

SHOW_2:
    MOV AX, 0132H       ; 显示字符 '2'
    INT 10H
    JMP DONE

SHOW_1:
    MOV AX, 0131H       ; 显示字符 '1'
    INT 10H

DONE:
    MOV AX, 0120H       ; space
    INT 10H
    MOV AL, 20H
    OUT 20H, AL
    IRET

CODE ENDS
END  START
```
