## 8251 控制字的写入
由于 8251 有两个控制命令字, 而控制命令字端口地址只有一个, 所以在初始化编程时, 命令控制字的写入是有顺序的
1. 芯片复位后, 首先往控制端口写入的是方式选择控制字
2. 如果在方式选择控制字中, 规定 8251 是同步工作方式, 则接下来向控制端口写入 1 个或 2 个同步字符(异步方式跳过该步骤即可)
3. 然后就是从控制端口写入操作命令控制字


## 实验内容(附加要求)
双机通讯实验，使用两台实验装置，一台为发送机，一台为接收机，进行两机间的串行通讯，实验步骤如下：
1. 按图 1 连接好电路，其中 8254 计数器如图 2，用于产生 8251 的发送和接收时钟，TXD 和 RXD 连在一起。
2. 波特率因子若选 16，计数器 2 初值为 12。则波特率=1.8432MHZ/12/16=9600bps
3. 编程:在发送机 3000H～3009H 内存单元写入ASCII 值：30，31，32，33，34，35，36，37，38，39 共 10 个数。将这 10 个数发送给接收机后显示在屏幕上，收发采用查询方式。


<img src="./images/8251 双机通讯实验接线图.png" alt="linking diagram" width="50%">


## 实验操作源码
### 发送端
```asm
OY0            EQU  0600H        
IOY1           EQU  0640H    
M8251_DATA     EQU  0600H            ;8251 的数据线
M8251_CON      EQU  0602H            ;8251 的控制端
M8254_2        EQU  0644H            ;写计数器 2
M8254_CON      EQU  0646H            ;写控制器

SSTACK    SEGMENT STACK
        DW 64 DUP(?)
SSTACK    ENDS

CODE    SEGMENT
        ASSUME CS:CODE
START:          
        MOV AL, 0B6H             ;计数器 2, 读写 16 位, 方式 3, 二进制计数 >>> 1010 0110
        MOV DX, M8254_CON
        OUT DX, AL
        MOV AL, 0CH              ;给计数器 2 赋初值 12 -- 低位
        MOV DX, M8254_2
        OUT DX, AL
        MOV AL, 00H              ;高位
        OUT DX, AL

        CALL INIT                ;复位 8251
        CALL DALLY

        MOV  AL, 0CEH            ;写方式控制字
        MOV  DX, M8251_CON
        OUT  DX, AL                
        CALL DALLY
        MOV AL, 31H              ;写操作命令字
        OUT DX, AL                
        CALL DALLY

        MOV DI, 3000H             ;存入 ASCⅡ 值的起始单元为 3000H
        MOV CX, 000AH             ;设置计数值 10
        PUSH AX
        MOV AL,30H           
A0:     MOV [DI], AL              ;向 3000H~3009H 单元中写数
        INC AL                    ;从 0 加到 9   
        INC DI                    ;地址后移一位
        LOOP A0
        POP AX

        MOV DI, 3000H             ;发送数据的起始地址
        MOV CX, 000AH             ;设定计数初值为 10
A1:        
        MOV AL, [DI]
        CALL SEND                 ;调用发送子程
        CALL DALLY
        INC DI
        LOOP A1
A2:        
        JMP A2

;复位8251子程序
INIT:    
        MOV AL, 00H                        
        MOV DX, M8251_CON
        OUT DX, AL
        CALL DALLY

        OUT DX, AL
        CALL DALLY

        OUT DX, AL
        CALL DALLY

        MOV AL, 40H  
        OUT DX, AL
        RET

DALLY:    
        PUSH CX
        MOV CX, 3000H
A4:        
        PUSH AX
        POP AX
        LOOP A4
        POP CX
        RET

;发送子程序
SEND:    
        PUSH DX                    
        PUSH AX               
        MOV  DX, M8251_CON
A3:        
        IN AL, DX               ;读状态寄存器
        AND AL, 01H             ;判断发送器是否准备好
        JZ A3               
        POP AX                  ;AL为要发送的值
        MOV DX, M8251_DATA    
        OUT DX, AL              ;发送数据
        MOV AH, 01H
        INT 10H                 ;显示数据
        POP DX
        RET
CODE    ENDS
        END     START
```

### 接收端
```asm
IOY0         EQU  0600H        
IOY1         EQU  0640H    
M8251_DATA   EQU  0600H            ;8251 的数据线
M8251_CON    EQU  0602H            ;8251 的控制端
M8254_2      EQU  0644H            ;写计数器 2
M8254_CON    EQU  0646H            ;写控制器
SSTACK    SEGMENT STACK
        DW 64 DUP(?)
SSTACK    ENDS
CODE    SEGMENT
        ASSUME CS:CODE
START:    
        ; 初始化 8254
        MOV AL, 0B6H             ;计数器 2，读写 16 位，方式 3，二进制计数
        MOV DX, M8254_CON
        OUT DX, AL
        MOV AL, 0CH              ;给计数器 2 赋初值 12 -- 低位
        MOV DX, M8254_2
        OUT DX, AL
        MOV AL, 00H              ;高位
        OUT DX, AL

        CALL INIT                ;复位 8251        
        CALL DALLY
        
        MOV  AL, 0CEH            ;写方式控制字
        MOV  DX, M8251_CON                
        OUT  DX, AL  
        CALL DALLY

        MOV  AL, 16H              ;写操作命令字
        OUT  DX, AL 
        CALL DALLY

        MOV DI, 3000H
        MOV CX, 000AH
A1:     
        MOV DX,M8251_CON         ;读状态寄存器
        IN  AL, DX
        AND AL, 02H              ;判断接收器是否准备好
        JZ A1          
        MOV DX, M8251_DATA
        IN  AL, DX              ;将接收的值赋给 AL
        MOV AH, 01H              ;显示接受到的值
        INT 10H
        MOV [DI],AL            ;将接收的值存入内存
        INC DI
        LOOP A1
        MOV AH, 4CH 
        INT 21H

        ;复位8251子程序
INIT:    
        MOV AL, 00H            
        MOV DX, M8251_CON
        OUT DX, AL
        CALL DALLY

        OUT DX, AL
        CALL DALLY

        OUT DX, AL
        CALL DALLY

        MOV AL, 40H
        OUT DX, AL
        RET
DALLY:   
        PUSH CX
        MOV CX, 3000H
A3:        
        PUSH AX
        POP AX
        LOOP A3
        POP CX
        RET
CODE ENDS
END START
```

## 发送端源码要点分析

### 端口地址
- `EQU` 指令用于定义常量
- `M8251_DATA` 和 `M8251_CON` 分别指 8251 的数据端口和控制端口
- `M8254_2` 和 `M8254_CON` 分别指 8254 定时器的计数器 2 和控制端
```asm
OY0            EQU  0600H        
IOY1           EQU  0640H    
M8251_DATA     EQU  0600H            ;8251 的数据线
M8251_CON      EQU  0602H            ;8251 的控制端
M8254_2        EQU  0644H            ;写计数器 2
M8254_CON      EQU  0646H            ;写控制器
```

### 堆栈段
- 定义了堆栈段 `SSTACK`, 包含了 64 个字单元
- `DW 64 DUP(?)` 创建了 64 个未初始化的字单元, 用于堆栈
- 组合类型值会被 `Link.exe` 识别
- `SSTACK SEGMENT STACK` 中 `SSTACK` 为段名(具有段地址、偏移地址属性, 此时的偏移地址为零); `STACK` 为组合类型, 表示该段中的数据无需在代码段中进行初始化, 同时 `SS` 和 `SP` 也会自动进行初始化, 默认组合类型为 `NONE`(缺省型), 需要在代码段中进行数据的初始化
```asm
SSTACK    SEGMENT STACK
        DW 64 DUP(?)
SSTACK    ENDS
```

### 代码段初始化和计数器配置
- 8254 的方式控制字设置为 `1010 0110`: 计数值格式为二进制, 模式选择为方式 3 (方波发生器), 读写指示为 只读/写计数器的高字节, 计数器选择为计数器 2
- 设置计数器初值 `12`, 该 16 位数先写入低 8 位 `0CH`, 后写入高 8 位 `00H`
```asm
CODE    SEGMENT
        ASSUME CS:CODE
START:          
        MOV AL, 0B6H             ;计数器 2, 读写 16 位, 方式 3, 二进制计数 >>> 1010 0110
        MOV DX, M8254_CON
        OUT DX, AL
        MOV AL, 0CH              ;给计数器 2 赋初值 12 -- 低位
        MOV DX, M8254_2
        OUT DX, AL
        MOV AL, 00H              ;高位
        OUT DX, AL
```

### 复位 8251 和初始化控制字
- 方式控制字设为 `1100 1110`, 异步(x16), 数据位长度为 8 位, 无校验, 2 位停止位
- 操作命令字设为 `0011 0001`, 清除错误标志, 请求发送, 允许发送
```asm
CALL INIT                ;复位 8251
CALL DALLY

MOV  AL, 0CEH            ;写方式控制字
MOV  DX, M8251_CON
OUT  DX, AL                
CALL DALLY

MOV AL, 31H              ;写操作命令字
OUT DX, AL                
CALL DALLY
```

### 写入 ASCII 值
- `AL` 中暂时存储要写入内存的 ASCII 值, 循环次数为 `000AH`
```asm
        MOV DI, 3000H             ;存入 ASCII 值的起始单元为 3000H
        MOV CX, 000AH             ;设置计数值 10
        PUSH AX
        MOV AL,30H           
A0:     MOV [DI], AL              ;向 3000H~3009H 单元中写数
        INC AL                    ;从 0 加到 9   
        INC DI                    ;地址后移一位
        LOOP A0
        POP AX
```

### 发送数据
- 循环发送 `000AH` 个字符
- `CALL SEND` 调用 `SEND` 子程序发送数据
- `CALL DALLY` 调用 `DALLY` 子程序进行延时
- `A2: JMP A2` 无限循环, 程序在此处停止
```asm
        MOV DI, 3000H             ;发送数据的起始地址
        MOV CX, 000AH             ;设定计数初值为 10
A1:        
        MOV AL, [DI]
        CALL SEND                 ;调用发送子程
        CALL DALLY
        INC DI
        LOOP A1
A2:        
        JMP A2
```

### 复位子程序
- 第 1, 2 次复位命令 `00H` 的发送, 可以理解为分别重置方式控制字, 操作命令字; 第 3 次复位命令的发送, 可能是为了确保稳妥, 确保在复杂或异常情况下芯片能完全复位
- `MOV AL, 40H : OUT DX, AL` 写入操作命令字 `0100 0000`, 进行内部置位
```asm
INIT:    
        MOV AL, 00H                        
        MOV DX, M8251_CON
        OUT DX, AL
        CALL DALLY

        OUT DX, AL
        CALL DALLY

        OUT DX, AL
        CALL DALLY

        MOV AL, 40H  
        OUT DX, AL
        RET
```

### 延时子程序
- 使用 `PUSH`、`POP` 进行类似空操作的延时
```asm
DALLY:    
        PUSH CX
        MOV CX, 3000H
A4:        
        PUSH AX
        POP AX
        LOOP A4
        POP CX
        RET
```

### 发送子程序
- 检查状态寄存器中的发送就绪位`D0`
- 调用发送子程序 `SEND`前, `AL` 中存储的是所要发送的 ASCII 值(子程序寄存器传参)
- `MOV AH, 01H : INT 10H` 显示字符(BIOS 中断服务 INT 10H, 功能号 AH=01H), 之前 8254 的实验程序中赋值对象为 `AX` , 同时进行功能号(`AH`)和显示字符(`AL`)的写入操作
- `RET` 返回到调用此子程序的位置
```asm
;发送子程序
SEND:    
        PUSH DX                    
        PUSH AX               
        MOV DX, M8251_CON
A3:        
        IN AL, DX               ;读状态寄存器
        AND AL, 01H             ;判断发送器是否准备好
        JZ A3               
        POP AX                  ;AL 为要发送的值
        MOV DX, M8251_DATA    
        OUT DX, AL              ;发送数据
        MOV AH, 01H
        INT 10H                 ;显示数据
        POP DX
        RET
```

## 接收端源码要点分析

### 端口地址
- `M8251_DATA` 8251 的数据端口, 用于数据的收发
- `M8251_CON` 8251 的控制端口, 用于写入控制字和读取状态字
- `M8254_2` 8254 计数器 2 的端口, 用于设置计数器初值
- `M8254_CON` 8254 控制端口, 用于设置计数器的工作模式
```asm
M8251_DATA   EQU  0600H            ;8251 的数据线
M8251_CON    EQU  0602H            ;8251 的控制端
M8254_2      EQU  0644H            ;写计数器 2
M8254_CON    EQU  0646H            ;写控制器
```

### 8254 的初始化
- 8254 的方式控制字设置为 `1011 0110`: 计数值格式为二进制, 模式选择为方式 3 (方波发生器), 读写指示为 读/写计数器的 16 位数, 计数器选择为计数器 2
- 设置计数器初值 `12`, 该 16 位数先写入低 8 位 `0CH`, 后写入高 8 位 `00H`
```asm
; 初始化 8254
MOV AL, 0B6H             ;计数器 2，读写 16 位，方式 3，二进制计数 >>> 1011 0110
MOV DX, M8254_CON
OUT DX, AL
MOV AL, 0CH              ;给计数器 2 赋初值 12 -- 低位
MOV DX, M8254_2
OUT DX, AL
MOV AL, 00H              ;高位
OUT DX, AL
```

### 8251 控制字的初始化
- 方式控制字设为 `1100 1110`, 异步(x16), 数据位长度为 8 位, 无校验, 2 位停止位
- 操作控制字设为 `0001 0110`, 清除错误标志, 允许接收, 数据终端准备好
```asm
CALL INIT                ;复位 8251        
CALL DALLY

MOV  AL, 0CEH            ;写方式控制字
MOV  DX, M8251_CON                
OUT  DX, AL  
CALL DALLY

MOV  AL, 16H             ;写操作命令字
OUT  DX, AL 
CALL DALLY
```

### 主程序
- 检查状态寄存器中的接收就绪位 `D1`
- 当接收器准备好后, 从数据端口读取数据并显示再屏幕上, 同时存入内存
- `MOV AH, 01H : INT 10H` 显示字符(BIOS 中断服务 INT 10H, 功能号 AH=01H), 之前 8254 的实验程序中赋值对象为 `AX` , 同时进行功能号(`AH`)和显示字符(`AL`)的写入操作
- `NOV AH, 4CH : INT 21H` DOS 服务调用功能号 `4CH` (终止进程并返回控制给操作系统), 调用 DOS 中断 21H 服务中, 调用的指定的功能是 `4CH`
```asm
        MOV DI, 3000H
        MOV CX, 000AH
A1:     
        MOV DX, M8251_CON        ;读状态寄存器
        IN  AL, DX
        AND AL, 02H              ;判断接收器是否准备好
        JZ A1          
        MOV DX, M8251_DATA
        IN  AL, DX               ;将接收的值赋给 AL
        MOV AH, 01H              ;显示接受到的值
        INT 10H
        MOV [DI],AL              ;将接收的值存入内存
        INC DI
        LOOP A1
        MOV AH, 4CH 
        INT 21H
```

### 复位 8251 的子程序
- 第 1, 2 次复位命令 `00H` 的发送, 可以理解为分别重置方式控制字, 操作命令字; 第 3 次复位命令的发送, 可能是为了确保稳妥, 确保在复杂或异常情况下芯片能完全复位
- `MOV AL, 40H : OUT DX, AL` 写入操作命令字 `0100 0000`, 进行内部置位 
```asm
        ;复位8251子程序
INIT:    
        MOV AL, 00H            
        MOV DX, M8251_CON
        OUT DX, AL
        CALL DALLY

        OUT DX, AL
        CALL DALLY

        OUT DX, AL
        CALL DALLY

        MOV AL, 40H
        OUT DX, AL
        RET
```