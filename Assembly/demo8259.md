## 实验源码

```asm
;=========================================================
; 文件名: A82591.ASM
; 功能描述: 8259中断实验, 中断源为主片8259的IRQ7
;           每产生一次中断输出显示一个字符7
;=========================================================

SSTACK	SEGMENT STACK
		DW 32 DUP(?)
SSTACK	ENDS
CODE   	SEGMENT
	ASSUME CS:CODE
START: 	PUSH DS
	MOV AX, 0000H
	MOV DS, AX
	MOV AX, OFFSET MIR7		;取中断入口地址
	MOV SI, 003CH			;中断矢量地址
	MOV [SI], AX			;填IRQ7的偏移矢量
	MOV AX, CS				;段地址
	MOV SI, 003EH
	MOV [SI], AX			;填IRQ7的段地址矢量
	CLI
	POP DS

	;初始化主片8259
	MOV AL, 11H
	OUT 20H, AL				;ICW1
	MOV AL, 08H
	OUT 21H, AL				;ICW2
	MOV AL, 04H
	OUT 21H, AL				;ICW3
	MOV AL, 01H
	OUT 21H, AL				;ICW4
	MOV AL, 6FH				
	OUT 21H, AL             ;OCW1  0110 1111   开放4号中断串口用，7号中断实验用
	STI
AA1:	NOP
	JMP AA1		
MIR7:	STI
	CALL DELAY
	MOV AX, 0137H
	INT 10H					;显示字符7
	MOV AX, 0120H
	INT 10H
	MOV AL, 20H
	OUT 20H, AL				;中断结束命令
	IRET		
DELAY:	PUSH CX
	MOV CX, 0F00H
AA0:	PUSH AX
	POP  AX
	LOOP AA0
	POP CX
	RET		
CODE	ENDS
END  START
```

## 要点分析
### 设置中断向量
- 中断向量号 TYPE 与中断向量表的存储地址存在确切的计算方式
- 存储 CS 基址的地址单元为 TYPE * 4 + 2, 存储 IP 偏移量的地址单元为 TYPE * 4
- 其中 8259 拥有 IRQ0 至 IRQ7 八个中断请求信号（输入信号）, ICW2 设定的就是 IRQ0 所映射的中断向量号, 此后的 IRQ1 至 IRQ7 所映射的中断向量号依次类推, 自动连续映射
- 实例解释: 此代码中针对 ICW2 的设置为 `MOV AL, 08H : OUT 21H, AL`, 此时 IRQ0 所映射的中断向量号为 8, 所以类推可得 IRQ7 所映射的中断向量号为 15, 故计算可得存储 IP 的地址单元为 `15*4(003CH)`, 存储 CS 的地址单元为 `15*4+2(003EH)` 
- **中断向量表的起始地址**: x86 架构中的一种默认规定, 中断向量表(Interrupt Vector Table, IVT)在内存中的固定位置开始(00000H), 每个中断向量占用 4 个字节, 每个地址可以存储 1 个字节的数据
- **现场保护**: 由于 x86 架构针对于 IVT 的默认规定, 所以设置中断向量时, 需要刚开始将 DS 段寄存器调整为 `0000H`, 故在过程的始终, 会针对于 DS 进行 push、pop 操作，以保护现场
- CS:IP 配合使用、DS:SI 配合使用
- CLI: Clear Interrupts, 作用时禁止中断，防止在接下来的代码执行过程中发生中断
```asm
PUSH DS
MOV AX, 0000H
MOV DS, AX
MOV AX, OFFSET MIR7		;取中断入口地址
MOV SI, 003CH			;中断矢量地址
MOV [SI], AX			;填IRQ7的偏移矢量
MOV AX, CS				;段地址
MOV SI, 003EH
MOV [SI], AX			;填IRQ7的段地址矢量
CLI
POP DS
```
### 初始化主片8259
- **字的设置顺序**: ICW 的设置有特定的顺序: ICW1、ICW2、ICW3、ICW4。ICW 设置完成后，才能进行 OCW 的设置
- 8259A 可编程中断控制器 (PIC) 的命令端口和数据端口分别有特定的寄存器用于配置和控制中断
- **命令端口(20H)**: ICW1、OCW2、OCW3
- **数据端口(21H)**: ICW2 ICW3 ICW4 OCW1
- `MOV AL, 6FH : OUT 21H, AL` 开放 4 号中断可能存在预留用途, 例如串口通信
- STI: Set Interrupts, 指令使能中断, 允许 CPU 响应硬件中断请求
```asm
;初始化主片8259
MOV AL, 11H
OUT 20H, AL				;ICW1
MOV AL, 08H
OUT 21H, AL				;ICW2
MOV AL, 04H
OUT 21H, AL				;ICW3
MOV AL, 01H
OUT 21H, AL				;ICW4
MOV AL, 6FH				
OUT 21H, AL             ;OCW1  0110 1111   开放4号中断串口用，7号中断实验用
STI
```
### 中断处理循环
- NOP: No Operation, 无操作指令, 只是占用一个 CPU 周期, 通常用于时间延迟或对齐指令
- 创建了一个无限循环, 使程序在此处不停地重复执行 NOP 指令, 以等待中断或其他实践发生
```asm
AA1:	NOP
	JMP AA1
```
### 中断服务子程序
- 中断结束命令 `MOV AL, 20H : OUT 20H, AL`, 实际上是发送一个中断结束命令(EOI, End of Interrupt)给 8259A, 针对的是 OCW2
- IRET: Interrupt Return, 从当前中断服务历程返回到中断发生前的程序
```asm
MIR7:	STI
	CALL DELAY
	MOV AX, 0137H
	INT 10H					;显示字符 7
	MOV AX, 0120H
	INT 10H
	MOV AL, 20H
	OUT 20H, AL				;中断结束命令
	IRET		
DELAY:	PUSH CX
	MOV CX, 0F00H
```
### DELAY 延时子程序
- 将 AX 寄存器的值压入堆栈并立刻弹出，形成一个空操作，虽不对 AX 的内容进行任何改变, 但会占用CPU周期, 从而实现延迟效果。
```asm
DELAY:	PUSH CX
	MOV CX, 0F00H            ;设置循环次数
AA0:	PUSH AX
	POP  AX
	LOOP AA0
	POP CX
	RET	
```

### 中断过程分析
![8259单中断实验](/Assembly/images/8259%20单中断实验接线图.png)
- 其中每次按动单次脉冲单元中的开关 KK1+, 便会将中断信号通过 MIR7(Interrupt Request Line 7) 引脚输入给主 8259, 从而引起中断

## 附加要求
- 要求每按一次开关产生一次中断，在屏幕上显示一次 "7"。 （附加：直到满足规定次数结束）
- HLT: Halt, 指令使 CPU 停止执行, 进入休眠状态, 直到一个外部硬件中断、非屏蔽中断(NMI)或复位信号唤醒它。在这段时间, CPU不再执行任何指令, 能够降低功耗, 是一种节能措施。
```asm
;=========================================================
; 文件名: A82591.ASM
; 功能描述: 8259中断实验, 中断源为主片8259的IRQ7;
;           每产生一次中断输出显示一个字符7, 直到满足规定次数结束
;=========================================================
SSTACK SEGMENT STACK
DW 32 DUP(?)
SSTACK ENDS
CODE   SEGMENT
ASSUME CS:CODE, SS:SSTACK

; 定义计数器
COUNT DB 0

START:
 PUSH DS
 MOV AX, 0000H
 MOV DS, AX
 MOV AX, OFFSET MIR7 ; 取中断入口地址
 MOV SI, 003CH ; 中断矢量地址
 MOV [SI], AX ; 填IRQ7的偏移矢量
 MOV AX, CS ; 段地址
 MOV SI, 003EH
 MOV [SI], AX ; 填IRQ7的段地址矢量
 CLI
 POP DS

; 初始化主片8259
 MOV AL, 11H
 OUT 20H, AL ; ICW1
 MOV AL, 08H
 OUT 21H, AL ; ICW2
 MOV AL, 04H
 OUT 21H, AL ; ICW3
 MOV AL, 01H
 OUT 21H, AL ; ICW4
 MOV AL, 6FH ; OCW1
 OUT 21H, AL

 STI

AA1:
 NOP
 JMP AA1

MIR7:
 STI
 CALL DELAY
 
 ; 更新计数器
 INC COUNT
 ; 检查是否达到目标次数
 MOV AL, COUNT
 CMP AL, 5
 JGE END_PROGRAM

 MOV AX, 0137H
 INT 10H ; 显示字符7
 MOV AX, 0120H
 INT 10H
 
 MOV AL, 20H
 OUT 20H, AL ; 中断结束命令
 IRET

DELAY:
 PUSH CX
 MOV CX, 0F00H
AA0:
 PUSH AX
 POP AX
 LOOP AA0
 POP CX
 RET

END_PROGRAM:
 ; 显示完成信息，或其他处理
 MOV AX, 0148H ; 显示字符 'H'
 INT 10H
 MOV AX, 0161H ; 显示字符 'a'
 INT 10H
 MOV AX, 016CH ; 显示字符 'l'
 INT 10H
 MOV AX, 0174H ; 显示字符 't'
 INT 10H
 MOV AX, 0169H ; 显示字符 'i'
 INT 10H
 MOV AX, 016EH ; 显示字符 'n'
 INT 10H
 MOV AX, 0167H ; 显示字符 'g'
 INT 10H
 HLT ; 停机指令

CODE ENDS
END START

```

