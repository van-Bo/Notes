## 实验源码
```asm
;=========================================================
; 文件名: A82551.ASM
; 功能描述: A 口为输出，B 口为输入，将读入的数据输出显示
;           IOY0
;=========================================================

IOY0         EQU   0600H          ;片选 IOY0 对应的端口始地址
MY8255_A     EQU   IOY0+00H*2     ;8255 的 A 口地址
MY8255_B     EQU   IOY0+01H*2     ;8255 的 B 口地址
MY8255_C     EQU   IOY0+02H*2     ;8255 的 C 口地址
MY8255_MODE  EQU   IOY0+03H*2     ;8255 的控制寄存器地址

SSTACK	SEGMENT STACK
		DW 32 DUP(?)
SSTACK	ENDS
CODE	SEGMENT
		ASSUME CS:CODE
START:	MOV DX, MY8255_MODE
		MOV AL, 82H
		OUT DX, AL
AA1:	MOV DX, MY8255_B
		IN  AL, DX
		CALL DELAY
		MOV DX, MY8255_A
		OUT DX, AL
		JMP AA1
DELAY:	PUSH CX
		MOV CX, 0F00H
AA2:	PUSH AX
		POP  AX
		LOOP AA2
		POP  CX
		RET
CODE	ENDS
		END  START
```

```asm
;=========================================================
; 文件名: A82553.ASM
; 功能描述: 本实验使 8255 端口 A 工作在方式 0 并作为输出口，
;           端口B工作在方式 1 并作为输入口
;=========================================================

IOY0         EQU   0600H          ;片选 IOY0 对应的端口始地址
MY8255_A     EQU   IOY0+00H*2     ;8255 的 A 口地址
MY8255_B     EQU   IOY0+01H*2     ;8255 的 B 口地址
MY8255_C     EQU   IOY0+02H*2     ;8255 的C口地址
MY8255_MODE  EQU   IOY0+03H*2     ;8255 的控制寄存器地址

STACK1 	SEGMENT STACK
        DW 256 DUP(?)
STACK1 	ENDS
CODE 	SEGMENT
        ASSUME CS:CODE
START: 	MOV DX,MY8255_MODE         ;初始化8255工作方式
       	MOV AL,86H                 ;工作方式1，A口输出，B口输入
       	OUT DX,AL
       	MOV DX,MY8255_MODE         ;C口PC2置位
       	MOV AL,05H                 
       	OUT DX,AL
		PUSH DS
		MOV AX, 0000H
		MOV DS, AX
		MOV AX, OFFSET MIR7			;取中断入口地址
		MOV SI, 003CH				;中断矢量地址
		MOV [SI], AX				;填IRQ7的偏移矢量
		MOV AX, CS					;段地址
		MOV SI, 003EH
		MOV [SI], AX				;填 IRQ7 的段地址矢量
		CLI
		POP DS
		;初始化主片8259
		MOV AL, 11H
		OUT 20H, AL				;ICW1
		MOV AL, 08H
		OUT 21H, AL				;ICW2
		MOV AL, 04H
		OUT 21H, AL				;ICW3
		MOV AL, 01H
		OUT 21H, AL				;ICW4
		MOV AL, 6FH				;OCW1
		OUT 21H, AL
		STI       
AA1:	NOP
		JMP AA1
MIR7:  	PUSH AX
       	MOV DX,MY8255_B            ;读 B 口
       	IN  AL,DX
       	MOV DX,MY8255_A            ;写 A 口
       	OUT DX,AL
	   	MOV AL,20H
       	OUT 20H,AL       
       	POP AX  
       	IRET  
DELAY:	PUSH CX
		MOV CX, 0F00H
AA0:	PUSH AX
		POP  AX
		LOOP AA0
		POP CX
		RET
CODE 	ENDS
     	END START
```

## 实验要求


## 要点分析
